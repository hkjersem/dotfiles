# Executes commands at login post-zshrc.

# Execute code that does not affect the current session in the background.
{
    # Compile the completion dump to increase startup speed.
    dump_file="$HOME/.zcompdump"
    if [[ "$dump_file" -nt "${dump_file}.zwc" || ! -s "${dump_file}.zwc" ]]; then
        zcompile "$dump_file"
    fi

    # Set environment variables for launchd processes.
    if [[ "$OSTYPE" == darwin* ]]; then
        if [[ -z "$TMUX" ]]; then
            for env_var in PATH MANPATH; do
                launchctl setenv "$env_var" "${(P)env_var}"
            done
        fi
    fi
} &!

# Speed up zsh compinit by only checking cache once a day.
autoload -Uz compinit
if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
    compinit
    compdump
else
    compinit -C
fi

# Set zsh-autosuggestions color
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=214'

# Bind zsh-history-substring-search keys
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
